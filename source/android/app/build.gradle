apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "org.ajoberstar.grgit"

ext {
    // [grgit](http://ajoberstar.org/grgit/)
    git = grgit.open()
    gitVersionCode = git.log().size()
    gitVersionName = git.describe(tags: true)

    // [自动版本号发布](https://leonchen1024.com/2019/05/28/Automatic-Version-Code-Release/)
    def pattern = "-(\\d+)-g"
    def matcher = gitVersionName =~ pattern
    if (matcher) {
        // 以距离上个 tag 的提交次数为第三个版本号.
        gitVersionName = gitVersionName.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        gitVersionName = gitVersionName + ".0"
    }

    println "Build $gitVersionCode $gitVersionName"
}

android {

    compileSdkVersion 29

    defaultConfig {
        applicationId "xyz.whoam.k12.launcher"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode gitVersionCode
        versionName gitVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildName = "K12"
            def type = variant.buildType.name
            def releaseApkName = buildName + "_" + defaultConfig.versionName + '_' + type + '.apk'
            outputFileName = releaseApkName
        }
    }
}

dependencies {
//    https://github.com/YummyLau/PanelSwitchHelper
    implementation 'com.effective.android:panelSwitchHelper-androidx:1.3.6'
//    https://square.github.io/okhttp/
    implementation 'com.squareup.okhttp3:okhttp:3.12.11'
//    https://github.com/lingochamp/okdownload
    implementation 'com.liulishuo.okdownload:okdownload:1.0.7'
    implementation 'com.liulishuo.okdownload:sqlite:1.0.7'
    implementation 'com.liulishuo.okdownload:okhttp:1.0.7'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}